plugins {
    id "maven"
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
    id "org.jetbrains.kotlin.kapt" version "1.2.41"
    id "com.github.johnrengelman.shadow" version "1.2.4"
    id "flavor.pie.promptsign" version "1.0.2" apply false
}

def isJitpack = System.getenv("JITPACK") != null

if (!isJitpack) {
    apply plugin: "flavor.pie.promptsign"
}

group "de.randombyte"
version "3.0.0"

repositories {
    jcenter()
    maven { url "https://repo.spongepowered.org/maven/" }
    maven { url "https://jitpack.io" }
    maven { url "http://repo.bstats.org/content/repositories/releases" }
}

configurations {
    compile.extendsFrom shadow
    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.41"
    kapt "org.spongepowered:spongeapi:7.0.0"
    shadow("com.github.randombyte-developer.kosp:kosp:v1.9.2") { transitive = false }
    shadow "org.bstats:bstats-sponge:1.2"
    compile "com.github.randombyte-developer:byte-items:v2.2"
}

shadowJar {
    configurations = [project.configurations.shadow]

    relocate "kotlin", "de.randombyte.entityparticles.shaded.kotlin"
    relocate "de.randombyte.kosp", "de.randombyte.entityparticles.shaded.kosp"
    relocate "org.bstats", "de.randombyte.entityparticles.shaded.bstats"

    classifier = null // Remove "-all" suffix from output file name
}
build.dependsOn shadowJar
install.dependsOn shadowJar
if (!isJitpack) {
    signArchives.dependsOn shadowJar
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
